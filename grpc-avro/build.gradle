import com.github.davidmc24.gradle.plugin.avro.GenerateAvroProtocolTask
import com.github.davidmc24.gradle.plugin.avro.GenerateAvroSchemaTask

plugins {
	id 'java-library'
	id "com.github.davidmc24.gradle.plugin.avro" version "1.3.0"
}

repositories {
	mavenCentral()
}

dependencies {
	api 'org.apache.avro:avro-grpc:1.11.0'
}

def generateProtocol = tasks.register("generateProtocol", GenerateAvroProtocolTask) {
	source file("src/main/avro")
	include("**/*.avdl")
	outputDir = file("build/generated-avro-main-avpr")
}

tasks.register("generateSchema", GenerateAvroSchemaTask) {
	dependsOn generateProtocol
	source file("src/main/avro")
	source file("build/generated-avro-main-avpr")
	include("**/*.avpr")
	outputDir = file("build/generated-main-avro-avsc")
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/main/grpc'
			srcDirs 'build/generated/main/java'
		}
	}
}